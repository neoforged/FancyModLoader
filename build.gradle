plugins {
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'net.neoforged.licenser' version '0.7.2' apply false
    id 'com.diffplug.spotless' version '6.25.0' apply false
    id 'net.neoforged.gradleutils' version "${gradleutils_version}"
    id 'net.neoforged.gradleutils.spotless' version "${gradleutils_version}" apply false
}

changelog {
    disableAutomaticPublicationRegistration()
}

gradleutils.version {
    branches {
        suffixExemptedBranches.add("7.x")
        suffixBranch()
    }
    tags {
        label = 'alpha'
    }
}

println "Version: ${project.version = gradleutils.version}"

tasks.named('updateDaemonJvm') {
    jvmVersion = JavaVersion.VERSION_21
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'net.neoforged.licenser'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'net.neoforged.gradleutils'
    apply plugin: 'net.neoforged.gradleutils.spotless'

    gradleutils {
        setupSigning(signAllPublications: true)
        changelog {
            from '7.0'
        }
    }

    group = 'net.neoforged.fancymodloader'
    version = rootProject.version

    spotlessUtils.configure(spotless)

    dependencyUpdates.rejectVersionIf { isNonStable(it.candidate.version) }

    java {
        withSourcesJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(Integer.valueOf(java_version))
            if (java_vendor) {
                vendor = JvmVendorSpec.matching(java_vendor)
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}

subprojects { subProject ->
    subProject.version = rootProject.version

    apply plugin : fmlbuild.InDevModulePlugin

    jar {
        manifest.attributes(
                'Git-Commit'  : gradleutils.gitInfo.abbreviatedId,
                'Build-Number': "${subProject.version}",
                'Automatic-Module-Name' : "fml_${subProject.name.replace("-", "_")}",
                'Specification-Title'   : "FML${subProject.name}",
                'Specification-Vendor'  : 'NeoForged',
                'Specification-Version' : "${subProject.version.toString().split('\\.')[0]}",
                'Implementation-Title'  : "FML $subProject.name",
                'Implementation-Version': "${subProject.version.toString().split('\\.')[0]}.${subProject.version.toString().split('\\.')[1]}",
                'Implementation-Vendor' : 'NeoForged'
        )

        if (subProject.name != 'junit') {
            manifest.attributes.put('FMLModType', 'LIBRARY')
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
    }

    license {
        header = rootProject.file('LICENSE-header.txt')
        skipExistingHeaders true

        include 'net/neoforged/'
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven gradleutils.publishingMaven
        }
    }
}
