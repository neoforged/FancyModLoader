plugins {
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'org.javamodularity.moduleplugin' version '1.8.7'
    id 'org.cadixdev.licenser' version '0.6.0'
    id 'net.neoforged.gradleutils' version '[2.0.18,3)'
}

changelog {
    disableAutomaticPublicationRegistration()
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'net.neoforged.gradleutils'

    group = 'net.neoforged.fancymodloader'
    version = gradleutils.getTagOffsetVersion()

    repositories {
        mavenCentral()

        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }

        maven {
            name = 'Minecraft'
            url = 'https://libraries.minecraft.net'
        }
    }

    dependencyUpdates.rejectVersionIf { isNonStable(it.candidate.version) }

    java.withSourcesJar()

    test {
        useJUnitPlatform()
    }
}

subprojects { subProject ->
    subProject.version = gradleutils.getTagOffsetVersion()
    ext {
        MANIFESTS = [
                ''                              : [
                ] as LinkedHashMap
        ]
    }

    jar.doFirst {
        manifest.attributes(
                'Git-Commit'  : gradleutils.gitInfo.abbreviatedId,
                'Build-Number': "${subProject.version}",
                'Automatic-Module-Name' : "fml_${subProject.name.replace("-", "_")}",
                'FMLModType'  : subProject.name.startsWith("language") ? 'LANGPROVIDER' : subProject.name == "events" ? 'GAMELIBRARY' : 'LIBRARY',
                'Specification-Title'   : "FML${subProject.name}",
                'Specification-Vendor'  : 'NeoForged',
                'Specification-Version' : "${subProject.version.split('\\.')[0]}",
                'Implementation-Title'  : "FML $subProject.name",
                'Implementation-Version': "${subProject.version.split('\\.')[0]}.${subProject.version.split('\\.')[1]}",
                'Implementation-Vendor' : 'NeoForged'
        )
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
    }

    license {
        header = rootProject.file('LICENSE-header.txt')

        include 'net/minecraftforge/'
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven gradleutils.getPublishingForgeMaven()
        }
    }

    if (System.getenv('GPG_PRIVATE_KEY')) {
        signing {
            final signingKey = System.getenv('GPG_PRIVATE_KEY') ?: ''
            final signingPassword = System.getenv('GPG_KEY_PASSWORD') ?: ''
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.maven
        }
    }

    changelog {
        fromTag "47.1"
    }
}