plugins {
    id 'net.minecraftforge.gradleutils' version '2.+'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.javamodularity.moduleplugin' version '1.8.3'
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

group 'cpw.mods'

version = gradleutils.getTagOffsetVersion()
logger.lifecycle('Version: ' + version)

repositories {
    mavenLocal()
    maven {
        name 'forge'
        url 'https://maven.minecraftforge.net/'
    }
}
dependencies {
    implementation('cpw.mods:securejarhandler:0.9.54')
}


task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

changelog {
    fromTag "0.1"
}

jar {
    manifest {
        attributes(
                'Specification-Title': 'bootstraplauncher',
                'Specification-Vendor': 'forge',
                'Specification-Version': '1', // We are version 1 of ourselves
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${System.getenv("BUILD_NUMBER")?:0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor':'forge',
                'Implementation-Timestamp': java.time.Instant.now().toString(),
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Build-Number': "${System.getenv("BUILD_NUMBER")?:0}",
                'Main-Class': 'cpw.mods.bootstraplauncher.BootstrapLauncher'
        )
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'Bootstrap launcher'
                description = 'Allows bootstrapping a modularized environment from a classpath one'
                url = 'https://github.com/McModLauncher/bootstraplauncher'
                scm {
                    url = 'https://github.com/McModLauncher/bootstraplauncher'
                    connection = 'scm:git:git@github.com:McModLauncher/bootstraplauncher.git'
                    developerConnection = 'scm:git:git@github.com:McModLauncher/bootstraplauncher.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/McModLauncher/bootstraplauncher/issues'
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (System.env.MAVEN_USER) {
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER ?: 'not'
                    password = System.env.MAVEN_PASSWORD ?: 'set'
                }
                url 'https://maven.minecraftforge.net/releases'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}


