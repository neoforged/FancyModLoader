plugins {
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.4'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
}

group 'cpw.mods'

version = gradleutils.version
logger.lifecycle("Version: {}", version)

repositories {
    mavenLocal()
    maven {
        name 'forge'
        url 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    compileOnly('org.jetbrains:annotations:24.0.1')
    implementation('cpw.mods:securejarhandler:3.0.4')
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

changelog {
    from '1.1'
}

jar {
    manifest {
        attributes(
                'Specification-Title': 'bootstraplauncher',
                'Specification-Vendor': 'mcmodlauncher',
                'Specification-Version': '1', // We are version 1 of ourselves
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor':'mcmodlauncher',
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Main-Class': 'cpw.mods.bootstraplauncher.BootstrapLauncher'
        )
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        pom {
            name = 'Bootstrap launcher'
            description = 'Allows bootstrapping a modularized environment from a classpath one'
            pomUtils.githubRepo(it, 'bootstraplauncher', 'McModLauncher')
            developers {
                developer {
                    id = 'cpw'
                    name = 'cpw'
                }
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}


