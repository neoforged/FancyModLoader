plugins {
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'net.minecraftforge.gradleutils' version '2.+'
    id 'me.champeau.jmh' version '0.6.5'
    id 'org.javamodularity.moduleplugin' version '1.8.7' apply false
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'org.javamodularity.moduleplugin'


repositories {
    mavenCentral()
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// We can't use toolchains because we need --add-export
//java.toolchain.languageVersion = JavaLanguageVersion.of(16)
compileJava {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

test {
    //exclude '**/*'
    useJUnitPlatform()
}

group = 'cpw.mods'

version = gradleutils.getTagOffsetVersion()
logger.lifecycle('Version: ' + version)

ext.asmVersion = 9.1
dependencies {
    api("org.ow2.asm:asm:${asmVersion}")
    api("org.ow2.asm:asm-tree:${asmVersion}")
    api("org.ow2.asm:asm-commons:${asmVersion}")
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.+')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.+')
}

changelog {
    fromTag "0.9"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.named('compileJava') {
    options.compilerArgs << '-Xlint:unchecked'
    moduleOptions {
        addExports = ['java.base/sun.security.util': 'cpw.mods.securejarhandler']
    }
}

jmh {
    benchmarkMode = ['avgt' ]
    profilers = [ 'stack', 'jfr:dir=./build/results/jmh/' ]
    timeOnIteration = '3s'
    warmup = '2s'
    warmupIterations = 3
    iterations = 3
    fork = 3
    timeUnit = 'us'
    // JMH loads the main sourceset without a module context, so add exports and opens for required stuff to all unnamed and disable the missing module crash
    jvmArgsPrepend = ['--add-opens java.base/java.util.jar=ALL-UNNAMED --add-exports java.base/sun.security.util=ALL-UNNAMED -Dsecurejarhandler.throwOnMissingModule=false']
}

tasks['jmh'].dependsOn(clean)

jar {
    manifest {
        attributes(
                'Specification-Title': 'securejarhandler',
                'Specification-Vendor': 'forge',
                'Specification-Version': '1', // We are version 1 of ourselves
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${System.getenv('BUILD_NUMBER')?:0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor':'forge',
                'Implementation-Timestamp': java.time.Instant.now().toString(),
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Build-Number': "${System.getenv("BUILD_NUMBER")?:0}",
        )
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'Secure Modular Jar handler'
                description = 'Making the Java modular system provide security information'
                url = 'https://github.com/McModLauncher/securejarhandler'
                scm {
                    url = 'https://github.com/McModLauncher/securejarhandler'
                    connection = 'scm:git:git://github.com/McModLauncher/securejarhandler.git'
                    developerConnection = 'scm:git:git@github.com:McModLauncher/securejarhandler.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/McModLauncher/securejarhandler/issues'
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}